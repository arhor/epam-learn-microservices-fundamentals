plugins {
    id "epam-learn-microservices-fundamentals.kotlin-conventions"
    id "io.spring.dependency-management"
    id "org.jetbrains.kotlin.plugin.spring"
    id "org.springframework.boot"
}

configurations {
    testImplementation {
        exclude module: "junit-vintage-engine"
    }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.mockito.kotlin:mockito-kotlin:${property('app.version.mockitoKotlin')}"
}

task contractTest(type: Test) {
    group = "verification"
    useJUnitPlatform {
        includeTags "contract"
    }
    shouldRunAfter test
}

task integrationTest(type: Test) {
    group = "verification"
    useJUnitPlatform {
        includeTags "integration"
    }
    shouldRunAfter contractTest
}

test {
    useJUnitPlatform {
        excludeTags "contract", "integration"
    }
}

check {
    dependsOn = [
        test,
        contractTest,
        integrationTest,
    ]
}
